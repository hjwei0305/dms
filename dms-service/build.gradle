apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// 扩展定义
ext {
    // 启动类
    MainClass = 'com.changhong.sei.RestApplication'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // 本项目依赖模块
    compile project(':dms-api')
    compile project(':multilingual-sdk')
    // SEI平台依赖包
    compile("com.changhong.sei:sei-cloud-zookeeper-starter:$sei_version")
    // edm sdk
    compile("com.changhong.sei:sei-edm-sdk:$sei_version")
    // 直接引用消息通知包
    compile("com.changhong.sei:sei-notify-sdk:$sei_version")
    // easyexcel
    compile("com.alibaba:easyexcel:2.2.6")
    // 第三方组件依赖包
    compile("mysql:mysql-connector-java:$mysqlVersion")
    compile("com.tencentcloudapi:tencentcloud-sdk-java:$tencentCloudVersion")
    compile("com.belerweb:pinyin4j:$pinyinVersion")

    // 测试依赖包
    // compile("com.changhong.sei:sei-test-starter:$sei_version")
    testCompile("com.changhong.sei:sei-test-starter:$sei_version")
}

// 处理资源配置文件
processResources {
    // 替换bootstrap.yaml文件中@project_version@占位符的值
    filesMatching('bootstrap.yaml') {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                project_name       : project.property('name'),
                project_version    : project.property('version'),
                project_description: project.property('description')
        ]
    }
    // 默认application文件中的配置从配置中心获取
    excludes = ["**/application.yaml", "**/application.properties"]
}

// 打ar包
bootJar  {
    enabled = true
    archiveName = 'dms.jar'
    mainClassName = MainClass
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.rootProject.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Description": "$project.description")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

//上传到nexus
uploadArchives {
    enabled = false
}

install {
    enabled = false
}

